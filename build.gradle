

buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }

}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


group = "com.stryphic" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "StryphicCore"

ext.configFile = file "build.properties"
configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}


sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    deployerJars
}
dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"

}
def build_number = project.config.build_number
version = "${project.config.mod_version}."+build_number

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "file://c/xampp/htdocs")
            pom {
                groupId = project.group
                version = "${project.version}"
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description project.archivesBaseName
                    url 'https://github.com/Stryphic-Team/'+project.archivesBaseName

                    scm {
                        url 'https://github.com/Stryphic-Team/'+project.archivesBaseName
                        connection 'scm:git:git://github.com/Stryphic-Team/'+project.archivesBaseName+'.git'
                        developerConnection 'scm:git:git@github.com:Stryphic-Team/'+project.archivesBaseName+'.git'
                    }

                    issueManagement {
                        system 'github'
                        url 'https://github.com/Stryphic-Team/'+project.archivesBaseName+'/issues'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://raw.githubusercontent.com/Stryphic-Team/StryphicCore/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'deastl'
                            name 'deastl'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}


